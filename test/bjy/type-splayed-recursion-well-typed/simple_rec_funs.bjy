
(*
  Here we just have many generic recursive functions that
  we're able to prove well-typed.
  Use the parallelism and type splaying flags.

  We even can have refinements on a lot of them.
*)

let rec summate (n : int | n >= 0) : int =
  if n == 0 
  then 0 
  else n + summate (n - 1)

let rec gcd (dependent a : int | a > 0) (dependent b : int | b > 0) : { x : int | x > 0 && x <= a && x <= b } =
  let rem = a % b in
  if rem == 0
  then b
  else gcd b rem

let lcm (dependent n : int | n > 0) (dependent m : int | m > 0) : { x : int | x >= n && x >= m } =
  n * m / gcd n m

let non_neg = { i : int | i >= 0 }

let pos_int = { i : int | i > 0 }

let rec range (dep a : non_neg) (b : non_neg | b >= a) : list int =
  if a == b
  then []
  else a :: range (a + 1) b

let reverse (type a) (ls : list a) : list a =
  let rec rev (acc : list a) (ls : list a) : list a =
    match ls with
    | [] -> acc
    | hd :: tl -> rev (hd :: acc) tl
    end
  in
  rev [] ls

(* Any refinements for orderedness will not work because they are recursive,
   so here we just use OCaml-like types. *)
let rec is_ordered (type a) (ls : list a) (cmp : a -> a -> int) : bool =
  match ls with
  | [] -> true
  | x :: tl ->
    match tl with
    | [] -> true
    | y :: _ -> cmp x y < 1 && is_ordered a tl cmp
    end
  end

let rec split_list (type a) (ls : list a) (n : non_neg) : { first_n : list a ; remaining : list a } =
  if n == 0
  then { first_n = [] ; remaining = ls }
  else
    match ls with
    | [] -> { first_n = [] ; remaining = [] }
    | hd :: tl ->
      let r = split_list a tl (n - 1) in
      { first_n = hd :: r.first_n ; remaining = r.remaining }
    end

let rec list_length (type a) (ls : list a) : non_neg =
  match ls with
  | [] -> 0
  | _ :: tl -> 1 + list_length a tl
  end

let rec merge (type a) (p : list a) (q : list a) (cmp : a -> a -> int) : list a =
  match p with
  | [] -> q
  | x :: p_tl ->
    match q with
    | [] -> p
    | y :: q_tl ->
      if cmp x y < 0
      then x :: merge a p_tl q cmp
      else y :: merge a p q_tl cmp
    end
  end

(*
  We cannot verify that this correctly sorts if we are type splaying.
  But simple ocaml-like types like this work well.

  Note that because this calls wrapped functions above, this is testing
  that we have correctly wrapped stubbed recursive types.
*)
let rec merge_sort (type a) (ls : list a) (cmp : a -> a -> int) : list a =
  match ls with
  | [] -> []
  | hd :: tl ->
    match tl with 
    | [] -> [ hd ]
    | _ ->
      let r = split_list a ls (list_length a ls / 2) in
      let sorted_left = merge_sort a r.first_n cmp in
      let sorted_right = merge_sort a r.remaining cmp in
      merge a sorted_left sorted_right cmp
    end
  end