
let (coord : type) =
  { x : int
   ; y : int }

let (t : type) = Mu tt.
  { parent : (``Some tt || ``None int)
   ; relative_coord : coord }

let is_coord_nonzero c = 
  not (c.x == 0) and not (c.y == 0)

let coord_equal a b =
  a.x == b.x and a.y == b.y

let has_parent x = 
  match x.parent with
  | `Some ignore -> true
  | _ -> false
  end

letd rec get_abs_coord (r : t)
  : 
  { abs_coord : coord |
    match r.parent with
    | `Some p ->
      if is_coord_nonzero (get_abs_coord p) # Note: recursive call here
      then not (coord_equal abs_coord r.relative_coord)
      else true
    | `None ignore -> true
    end
  }
  =
  match r.parent with
  | `Some parent -> 
    let c = parent.relative_coord in # ERROR: should be `get_abs_coord parent`
    { x = c.x + r.relative_coord.x ; y = c.y + r.relative_coord.y }
  | `None ignore -> r.relative_coord
  end
